plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id("com.google.devtools.ksp").version("1.8.22-1.0.11")
  id 'project-report' // run ./gradlew htmlDependencyReport
  id "com.vanniktech.maven.publish.base"
}

//https://issuetracker.google.com/issues/226095015
com.android.tools.analytics.AnalyticsSettings.setOptedIn(false)

task sourceJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
//  classifier "sources"
}

android {
  namespace 'app.cash.paykit.core'
  compileSdk versions.compileSdk

  defaultConfig {
    minSdk versions.minSdk
    // We target the minimum API that meets Google Play's target level, for higher compatibility: https://developer.android.com/google/play/requirements/target-sdk
    //noinspection OldTargetApi
    targetSdk versions.targetSdk

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
    resValue("string", "cap_version", "$version")
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
  }
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_17.toString()
  }
//  kotlin {
//    jvmToolchain(17)
//  }
  resourcePrefix 'cap_'

  lintOptions {
    abortOnError true
    htmlReport true
    checkAllWarnings true
    warningsAsErrors true
    baseline file("lint-baseline.xml")
  }

  testOptions {
    unitTests {
      returnDefaultValues = true
      includeAndroidResources = true
    }
  }
}

dependencies {
  implementation('app.cash.paykit.state:paykit:+')
//  implementation project(":PayKitLib")
//  implementation project(path: ':PayKitLib')

  implementation("io.github.nsk90:kstatemachine:+") // this is not coming in transitively?

  // KotlinX Dates.
  implementation "org.jetbrains.kotlinx:kotlinx-datetime:$kotlinx_date_version"
  implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0") // TODO why didn't this dep from PayKitLib get picked up transitively?

  // Provides a lifecycle for the whole application process.
  implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"

  // AndroidX Startup.
  implementation "androidx.startup:startup-runtime:$startup_version"

  //noinspection GradleDependency
  implementation "com.squareup.okhttp3:okhttp:$okhttp_version"

  implementation project(':logging')
  implementation project(':analytics-core')

  // TEST RELATED.

  testImplementation "junit:junit:$junit_version"
  testImplementation "io.mockk:mockk:$mockk_version"
  testImplementation "com.google.truth:truth:$google_truth_version"
  androidTestImplementation "androidx.test.ext:junit-ktx:$junit_androidx_version"
  testImplementation "com.squareup.okhttp3:mockwebserver:$mockwebserver_version"
  // Robolectric environment.
  testImplementation "org.robolectric:robolectric:$robolectric_version"
  // Coroutines test support.
  testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_test_version"
  //Test helpers for Lifecycle runtime
  testImplementation "androidx.lifecycle:lifecycle-runtime-testing:$lifecycle_version"
}

mavenPublishing {
  // AndroidMultiVariantLibrary(publish a sources jar, publish a javadoc jar)
  configure(new com.vanniktech.maven.publish.AndroidSingleVariantLibrary("release", true, true))
}