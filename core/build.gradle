plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id 'project-report' // run ./gradlew htmlDependencyReport
  id "com.vanniktech.maven.publish.base"
  id "org.jetbrains.kotlin.kapt"
}

task sourceJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier "sources"
}

android {
  namespace 'app.cash.paykit.core'
  compileSdk versions.compileSdk

  defaultConfig {
    minSdk versions.minSdk
    // We target the minimum API that meets Google Play's target level, for higher compatibility: https://developer.android.com/google/play/requirements/target-sdk
    //noinspection OldTargetApi
    targetSdk versions.targetSdk

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
    resValue("string", "cap_version", "$version")
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8

    coreLibraryDesugaringEnabled true
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }

  resourcePrefix 'cap_'

  lintOptions {
    abortOnError true
    htmlReport true
    checkAllWarnings true
    warningsAsErrors true
    baseline file("lint-baseline.xml")
  }

  testOptions {
    unitTests {
      returnDefaultValues = true
      includeAndroidResources = true
    }
  }
}

dependencies {

  //noinspection GradleDependency
  implementation("com.squareup.moshi:moshi-kotlin:$moshi_version")
  kapt("com.squareup.moshi:moshi-kotlin-codegen:$moshi_version")

  // Provides a lifecycle for the whole application process.
  implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"

  // AndroidX Startup.
  implementation "androidx.startup:startup-runtime:$startup_version"

  //noinspection GradleDependency
  implementation "com.squareup.okhttp3:okhttp:$okhttp_version"

  implementation project(':analytics-core')

  // TEST RELATED.

  testImplementation "junit:junit:$junit_version"
  testImplementation "io.mockk:mockk:$mockk_version"
  testImplementation "com.google.truth:truth:$google_truth_version"
  androidTestImplementation "androidx.test.ext:junit-ktx:$junit_androidx_version"
  testImplementation "com.squareup.okhttp3:mockwebserver:$mockwebserver_version"
  // Robolectric environment.
  testImplementation "org.robolectric:robolectric:$robolectric_version"
  // Coroutines test support.
  testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_test_version"
  //Test helpers for Lifecycle runtime
  testImplementation "androidx.lifecycle:lifecycle-runtime-testing:$lifecycle_version"

  //noinspection GradleDependency
  coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'
}

mavenPublishing {
  // AndroidMultiVariantLibrary(publish a sources jar, publish a javadoc jar)
  configure(new com.vanniktech.maven.publish.AndroidSingleVariantLibrary("release", true, true))
}